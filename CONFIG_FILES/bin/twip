#!/usr/bin/perl
# twip: TWitter IP (this file is part of Twip)
# Copyright 2010,2012 Elliot Wolk
# License: GNU GPL version 3 or any later version
use strict;
use warnings;
use Getopt::Long qw(GetOptionsFromArray);

my $usage = "Usage:
  $0 [OPTS] -g|--get [-a|--all] 
    Fetch the most recent tweet and decrypt it to an IP 
    -a|--all
      Fetches as many tweets as possible and decrypts them all
  $0 [OPTS] -p|--put [--ip IP]
    Obtain the external IP, encrypt it, and tweet it
    --ip IP
      Use IP instead of fetching from the internet
  $0 [OPTS] --decrypt BLOCK
    Instead of fetching, use this encrypted block,
    perhaps copied from twitter, and decrypt it directly
  $0 [OPTS] --fetch
    Get the ip using wget instead of OAUTH, and decrypt it like --decrypt
    Bypasses Net::Twitter completely
  $0 -h|--help
    Show this message
  OPTS:
    --config CONFIG 
      Use CONFIG instead of ~/.twip
";


sub parseOpts(@);
sub parseConfig($);
sub writeConfig($$);
sub getIP();
sub encryptIP($$);
sub decryptIP($$);
sub authorize($$$$);
sub fetchTweet($);

sub main(@){
  my %opts = parseOpts @_;

  my %config = parseConfig $opts{configFile};

  if(defined $opts{fetch}){
    my $block = fetchTweet $config{screenName};
    print decryptIP $block, $config{accessTokenSecret};
    exit 0;
  }

  my $auth = authorize(
    $config{consumerKey},
    $config{consumerSecret},
    $config{accessToken},
    $config{accessTokenSecret},
  );

  my $nt = $$auth[0];
  $config{accessToken} = $$auth[1];
  $config{accessTokenSecret} = $$auth[2];

  writeConfig $opts{configFile}, \%config;

  if(defined $opts{get}){
    my @statuses = @{$nt->user_timeline};
    if(@statuses <= 0){
      print "No statuses found!\n";
    }elsif(not defined $opts{all}){
      print decryptIP($statuses[0]->{text}, $config{accessTokenSecret});
    }else{
      for my $status(@statuses){
        my $ip = decryptIP($status->{text}, $config{accessTokenSecret});
        chomp $ip;
        my $date = $status->{created_at};
        print "$ip - $date\n";
      }
    }
  }elsif(defined $opts{put}){
    my $ip = $opts{useIP};
    $ip = getIP if not defined $ip;
    chomp $ip;

    my $msg = encryptIP($ip, $config{accessTokenSecret});
    $nt->update({ status => $msg });
  }elsif(defined $opts{decrypt}){
    print decryptIP $opts{decrypt}, $config{accessTokenSecret};
  }
}

sub parseOpts(@){
  my %opts;
  my $result = GetOptionsFromArray(\@_,
    "config=s"  => \$opts{configFile},
    "ip=s"      => \$opts{useIP},
    "all|a"     => \$opts{all},
    "get|g"     => \$opts{get},
    "put|p"     => \$opts{put},
    "decrypt=s" => \$opts{decrypt},
    "fetch"     => \$opts{fetch},
    "h|help"    => \$opts{help},
  );
  die $usage if defined $opts{help} or not $result;
  
  die $usage if defined $opts{all} and not defined $opts{get};
  die $usage if defined $opts{useIP} and not defined $opts{put};

  my $cmdCount = 0;
  $cmdCount++ if defined $opts{get};
  $cmdCount++ if defined $opts{put};
  $cmdCount++ if defined $opts{decrypt};
  $cmdCount++ if defined $opts{fetch};
  die $usage if $cmdCount != 1;
  
  $opts{configFile} = "$ENV{HOME}/.twip" if not defined $opts{configFile};
  return %opts;
}

sub parseConfig($){
  my $file = shift;
  open FH, "< $file" or die "Could not open $file for reading";
  my %config;
  my %allowedConfigItems = map {$_ => 1} qw(
    screenName
    accessToken
    accessTokenSecret
    consumerKey
    consumerSecret
  );
  for my $line(<FH>){
    chomp $line;
    $line =~ s/#.*//;
    next if $line =~ /^\s*$/;
    if($line =~ /^\s*([a-zA-Z0-9_-]+)\s*=\s*(.*)$/){
      if(not defined $allowedConfigItems{$1}){
        die "Unknown config item: $1\n";
      }
      $config{$1} = $2;
    }else{
      die "Malformed config file line: $line";
    }
  }
  close FH;
  return %config;
}

sub writeConfig($$){
  my $file = shift;
  my %config = %{shift()};
  my $out = '';
  for my $key(sort keys %config){
    $out .= "$key=$config{$key}\n";
  }
  open FH, "> $file" or die "Could not open $file for writing";
  print FH $out;
  close FH;
}

sub getIP(){
  my $ip = `extip`;
  chomp $ip;
  return $ip;
}
sub encryptIP($$){
  my $ip = shift;
  my $accessTokenSecret = shift;
  my $cmd = ''
    . "echo '$ip' | "
    . "gpg --no-use-agent -q -a --symmetric "
    . "  --passphrase \"gpgkey_$accessTokenSecret\"";
  my $block = `$cmd`;
  $block =~ s/^-----BEGIN PGP MESSAGE-----\n.*\n\n//;
  $block =~ s/\n-----END PGP MESSAGE-----\n*$//;
  $block =~ s/!/!!!/g;
  $block =~ s/\+/!p!/g;
  $block =~ s/ /!s!/g;
  $block =~ s/\t/!t!/g;
  $block =~ s/\n/!n!/g;
  return $block;
}
sub decryptIP($$){
  my $enc = shift;
  $enc =~ s/[+ \t\n]//g;
  my $accessTokenSecret = shift;
  
  $enc =~ s/!n!/\n/g;
  $enc =~ s/!t!/\t/g;
  $enc =~ s/!s!/ /g;
  $enc =~ s/!p!/+/g;
  $enc =~ s/!!!/!/g;
  
  my $block = "-----BEGIN PGP MESSAGE-----\n";
  $block .= "Version: GnuPG v1.4.9 (GNU/Linux)\n";
  $block .= "\n";
  $block .= $enc;
  $block .= "\n-----END PGP MESSAGE-----";
  my $cmd = ''
    . "echo '$block' | "
    . "gpg --no-use-agent --no-mdc-warning -q -d "
    . "  --passphrase \"gpgkey_$accessTokenSecret\"";

  return `$cmd`;
}

sub authorize($$$$){
  my $consumerKey = shift;
  my $consumerSecret = shift;
  my $accessToken = shift;
  my $accessTokenSecret = shift;

  require Net::Twitter;
  my $nt = Net::Twitter->new(
      traits           => ['API::RESTv1_1', 'OAuth'],
      consumer_key     => $consumerKey,
      consumer_secret  => $consumerSecret,
      legacy_lists_api => 0,
  );

  if($accessToken && $accessTokenSecret){
      $nt->access_token($accessToken);
      $nt->access_token_secret($accessTokenSecret);
  }

  unless($nt->authorized){
    # The client is not yet authorized: Do it now
    print "Authorize this app at " .
      $nt->get_authorization_url .
      " and enter the PIN#\n";

    my $pin = <STDIN>;
    chomp $pin;

    ($accessToken, $accessTokenSecret) =
      $nt->request_access_token(verifier => $pin);
  }

  if(not $nt->authorized){
    die "Could not authorize\n";
  }

  return [$nt, $accessToken, $accessTokenSecret];
}

sub fetchTweet($){
  my $screenName = shift;
  my $url = "http://twitter.com/$screenName";
  my $tweets = `wget -q -O - $url`;
  if($tweets =~ /<p class="[^"]*tweet-text[^"]*">([^<]*)<\/p>/s){
    return $1;
  }
  die "Twitter must have changed their html; fix me in twip.fetchTweet\n";
}

&main(@ARGV);
