#!/usr/bin/perl
use strict;
use warnings;
use File::Basename;

my %replacements = (
  'emumaster' => 'diskgallery',
  'settings' => 'duicontrolpanel',
  'grob' => 'browser',
);

my @ignoredPatterns = qw(
  web-search-*.desktop
);

my @config = qw(
  call-history
  messaging-ui
  gallery
  grob

  camera-ui
  mtermite
  mail
  settings

  nokia-drive-qml
  contacts
  clock
  calendar

  music-suite
  video-suite
  accountsui
  qtbtn-power

  dir-tools[
    call-log
    ovistoreclient
    contact-coocoo
    dontgosleep
    screenshotmee
  ]
  dir-utils[
    dropcache-mdn
    dcim-reindex
    restart-wifi
  ]
  dir-apps[
    fennec
    QmlMozBrowser
    FBReader
    joikuspot
    NokiaPt
    brujula
    meeseepics
  ]
  dir-misc[
    cameraplus
    modrana
    klomp
    qtbtn-klomp
    qtodo
    dingmee
    transmission
    qtbigtext
    teatime
    vmpkn9
    xmcr
    emumaster
    btinput-terminal
    btinput
    toggleBT
    finch
    pidgin
    n9-button-monitor
  ]
);

for my $entry(@config){
  if(defined $replacements{$entry}){
    $entry = $replacements{$entry};
  }
}

my @quickLaunch = qw(
  call-history
  messaging-ui
  fennec
  camera-ui
);


my %apps;
my %appDirs;
my %dirs;
my %dirNames;

my $appDir = '/usr/share/applications';
my $unusedDir = '/opt/unused-applications';
my $directoryDir = '/home/user/.local/share/applications';
my $quickLaunchDir = '/home/user/.local/share/applications';
my $confFileDest = '/home/user/.config/meegotouchhome-nokia/launcherbuttons.data';

sub run(@);
sub appendHarmattans();
sub parseConfig();
sub getValidDesktopNames();
sub launcherPath($);
sub createAppDesktopFiles();
sub createDirDesktopFiles();
sub createQuickLauncherDesktopFiles();
sub getAppData();
sub getDirData();
sub getQuickLauncherData();
sub getData();
sub writeLauncherButtonsData($);

#in case running in a gui terminal
$SIG{HUP} = sub { print STDERR "Caught a sighup $!" };

sub main(@){
  if(`which n9` ne "" and `hostname` ne `n9`){
    my $host = `n9`;
    chomp $host;
    my $name = basename $0;
    print "copying to the device\n";
    system "scp", $0, "root\@$host:/tmp/$name";
    print "rerunning on the device\n";
    exec "ssh", "root\@$host", "/tmp/$name", @_;
  }

  ensureRoot();

  if(@_ == 1 and $_[0] eq '-g'){
    print getValidDesktopNames();
    exit 0;
  }
  die "Usage: $0   or  $0 -g\n" if @_ > 0;

  run "/sbin/initctl stop xsession/mthome";
  appendHarmattans();
  parseConfig();
  createAppDesktopFiles();
  createDirDesktopFiles();
  createQuickLauncherDesktopFiles();

  my $data = getData();
  run "killall meegotouchhome";
  writeLauncherButtonsData($data);
  run "/sbin/initctl start xsession/mthome";
}

sub ensureRoot(){
  if(`whoami` ne "root\n"){
    print "rerunning as root\n";
    exec "sudo", $0, @ARGV;
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

sub appendHarmattans(){
  my %harmattans;
  for my $valid(getValidDesktopNames()){
    if($valid =~ m/^(.*)_harmattan$/){
      $harmattans{$1} = 1
    }
  }
  for(my $i=0; $i<@config; $i++){
    if(defined $harmattans{$config[$i]}){
      $config[$i] .= "_harmattan";
      print "appending '_harmattan' to $config[$i]\n";
    }
  }
  for(my $i=0; $i<@quickLaunch; $i++){
    if(defined $harmattans{$config[$i]}){
      $quickLaunch[$i] .= "_harmattan";
      print "appending '_harmattan' to $quickLaunch[$i]\n";
    }
  }
}

sub parseConfig(){
  my $index = 0;
  my $nextDir = 1;
  for(my $i=0; $i<@config; $i++){
    my $item = $config[$i];
    if($item =~ /^dir-(.*)\[$/){
      my $dirName = $1;
      $dirs{$nextDir} = $index++;
      $dirNames{$nextDir} = $dirName;

      my $dirIndex = 0;
      while(++$i<@config and $config[$i] ne ']'){
        my $app = $config[$i];
        $appDirs{$app} = $nextDir;
        $apps{$app} = $dirIndex++;
      }

      $nextDir++;
    }else{
      my $app = $config[$i];
      $apps{$app} = $index++;
    }
  }
}

sub getValidDesktopNames(){
  my @names = `ls $appDir/*.desktop $unusedDir/*.desktop`;
  for my $name(@names){
    $name =~ s/.desktop$//;
    $name =~ s/^.*\///;
  }
  return @names;
}

sub launcherPath($){
  my $path = shift;
  $path =~ s/^\///;
  $path =~ s/\//\\/g;
  return $path;
}


sub createAppDesktopFiles(){
  run "mkdir", "-p", $unusedDir;
  run "mv $appDir/*.desktop $unusedDir";
  run "rm -rf $appDir";
  run "mkdir", "-p", $appDir;
  for my $app(keys %apps){
    run "mv", "$unusedDir/$app.desktop", $appDir;
  }
  run "cd $unusedDir; mv @ignoredPatterns $appDir";
}

sub createDirDesktopFiles(){
  run "rm $directoryDir/*.directory";
  for my $dir(sort keys %dirs){
    my $dirName = $dirNames{$dir};
    my $content = ''
      . "[Desktop Entry]\n"
      . "Type=Directory\n"
      . "Name=$dirName\n"
      . "X-MeeGo-Folder-Index=$dir\n"
      . "Icon=icon-l-default-application\n"
      . "OnlyShowIn=X-MeeGo;\n"
      ;
    my $fileName = "$directoryDir/folder$dir.directory";
    open FH, "> $fileName" or die "Couldnt write to $fileName\n";
    print FH $content;
    close FH;
  }
  run "chown", "user.users", "-R", $directoryDir;
}

sub createQuickLauncherDesktopFiles(){
  run "rm $quickLaunchDir/*.desktop";
  for(my $i=0; $i<@quickLaunch; $i++){
    my $qlFile = "$quickLaunchDir/quicklaunchbar$i.desktop";
    run "ln", "-s", "$appDir/$quickLaunch[$i].desktop", $qlFile;
  }
  run "chown", "user.users", "-R", $quickLaunchDir;
}


sub getAppData(){
  my $data = '';
  for my $app(sort keys %apps){
    my $index = $apps{$app};
    my $dir = $appDirs{$app};
    $dir = 0 if not defined $dir;

    my $desktopFile = "$appDir/$app.desktop";
    $data .= launcherPath($desktopFile) . "=launcher/$dir/$index\n";
  }
  return $data;
}

sub getDirData(){
  my $data = '';
  for my $dir(sort keys %dirs){
    my $dirName = $dirNames{$dir};
    my $index = $dirs{$dir};
    my $desktopFile = "$directoryDir/folder$dir.directory";
    $data .= launcherPath($desktopFile) . "=launcher/0/$index\n";
  }
  return $data;
}

sub getQuickLauncherData(){
  my $data = '';
  for(my $i=0; $i<@quickLaunch; $i++){
    my $desktopFile = "$quickLaunchDir/quicklaunchbar$i.desktop";
    $data .= launcherPath($desktopFile) . "=quicklaunchbar/$i\n";
  }
  return $data;
}

sub getData(){
  my $data = "[DesktopEntries]\n";
  $data .= getDirData();
  $data .= getQuickLauncherData();
  $data .= getAppData();
  return $data;
}

sub writeLauncherButtonsData($){
  my $data = shift;
  print $data;

  open FH, "> $confFileDest" or die "Couldnt write to $confFileDest\n";
  print FH $data;
  close FH;
}

&main(@ARGV);
