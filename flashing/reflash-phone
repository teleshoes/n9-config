#!/usr/bin/perl
use strict;
use warnings;

sub flashAndRootStrap($$$);
sub attemptSSH();
sub attemptWifi();
sub slowInstalls();
sub run(@);
sub tryrun(@);

sub main(@){
  die "Usage: $0 FIRMWARE EMMC KERNEL\n" if @_ != 3;
  my ($fw, $emmc, $kernel) = @_;

  run "n9", "-t", "192.168.2.15";
  $|=1;
  flashAndRootStrap $fw, $emmc, $kernel;
  attemptSSH();
  chdir "../";
  attemptWifi();
  slowInstalls();
}

sub flashAndRootStrap($$$){
  my ($fw, $emmc, $kernel) = @_;

  $fw = readlink $fw if -l $fw;
  $emmc = readlink $emmc if -l $emmc;
  $kernel = readlink $kernel if -l $kernel;

  my $version = "\\d\\d.\\d\\d\\d\\d.\\d\\d-\\d";
  my $fwRegex = "DFL61_HARMATTAN_${version}_PR_LEGACY_00\\d-OEM1-958_ARM.bin";
  my $emmcRegex = "DFL61_HARMATTAN_${version}.[A-Z]+_EMMC_[A-Z]+.bin";
  my $kernelRegex = "zImage";
  die "weird firmware: $fw\n" if not -f $fw or $fw !~ /$fwRegex/;
  die "weird emmc: $emmc\n" if not -f $emmc or $emmc !~ /$emmcRegex/;
  die "weird kernel: $kernel\n" if not -f $kernel or $kernel !~ /$kernelRegex/;

  print "\n\n\nflashing firmware and emmc\n";
  run "sudo", "flasher", "-f", "-F", $fw, "-F", $emmc;

  print "\n\n\nflashing ubiboot and starting ubiboot maintenance console\n";
  run "./ubi-console", "--flash";

  print "\n\n\nmounting partitions and copying files for rootstrapping\n";
  tryrun "./ubi-mnt", "-u";
  tryrun "./ubi-mnt", "-u";
  run "./ubi-mnt", "-m";
  run "./ubi-rootstrap";
  run "./ubi-sshkeys";
  run "./ubi-mnt", "-u";

  print "\n\n\nrebooting and flashing kernel in the background\n";
  run "sudo flasher -f -F $fw -k $kernel --flash-only=kernel -R &";
  run "./ubi-cmd", "reboot2";
}

sub attemptSSH(){
  print "\n\n\nwaiting for kernel flash, rebooting, and attempting to ssh\n";
  my $usbUpCmd = "sudo ifconfig usb0 192.168.2.14";
  tryrun $usbUpCmd;
  if($? != 0){
    print "$usbUpCmd failed, quietly rerunning until success:\n";
    while(1){
      print ".";
      system "$usbUpCmd > /dev/null 2>/dev/null";
      last if $? == 0;
      sleep 1;
    }
  }
  print "usb network up!\n";

  run "ssh", "root\@192.168.2.15", "passwd -d user";
  my $out = `ssh user\@192.168.2.15 'whoami; hostname; echo YAY'`;
  if($out eq "user\nRM696\nYAY\n"){
    print "\n\n\nSUCCESSFUL SSH\n";
  }else{
    die "couldnt ssh\n";
  }
}

sub attemptWifi(){
  print "\n\nattempting to switch to wifi\n";
  print "waiting for icd2\n";
  while(1){
    print ".";
    system "n9 -s pgrep icd2 > /dev/null";
    last if $? == 0;
    sleep 1;
  }
  print "icd2 is runnning\n";

  run "n9", "-s", "/sbin/initctl stop xsession/icd2";
  run "./sync-wifi";
  run "n9", "-s", "/sbin/initctl start xsession/icd2";

  my $limit = 30;
  print "waiting for wifi, stopping after ${limit}s\n";
  my $start = time;
  while(1){
    my $wlan = `n9 -s ifconfig wlan0`;
    print ".";
    last if $wlan =~ /inet addr:\d+.\d+.\d+.\d+/;
    last if time-$start > $limit;
    sleep 1;
  }
  my $wlan = `n9 -s ifconfig wlan0`;
  if($wlan =~ /inet addr:\d+.\d+.\d+.\d+/){
    print "\n\n\nSUCCESSFUL WIFI!\n";
  }else{
    print "\n\n\nNO WIFI, continuing without\n";
  }
}

sub slowInstalls(){
  print "\n\nrunning time-consuming stuff\n";
  print "respawn-limit, packages {twice}, config-files\n";
  run "./respawn-limit";
  run "./packages.pl";
  run "./packages.pl";
  run "./config-files";
}

sub run(@){
  tryrun @_;
  die "@_ failed\n" if $? != 0;
}
sub tryrun(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
