#!/usr/bin/perl
use strict;
use warnings;

my $localDCIM = "$ENV{HOME}/Desktop/DCIM";
my $remoteDCIM = "/home/user/MyDocs/DCIM";

my @filetypes = qw(jpg jpeg mp4);

sub md5sumMaps(\@);
sub runMd5sum($);
sub updateMd5sums();
sub wrapQuotes($);

sub main(@){
  chdir $localDCIM;
  print "Getting md5sums of files in root of remote DCIM\n";
  my @remoteMd5sums = `n9 -s md5sum $remoteDCIM/*.* 2>/dev/null`;
  print "===all:\n" . join "", @remoteMd5sums;
  print "===\n\n";

  my ($byMd5sum, $byFile) = md5sumMaps @remoteMd5sums;

  my (@backedUp, @needsBackUp);
  my $localByMd5sum = updateMd5sums;
  for my $md5sum(keys %$byMd5sum){
    my $file = $$byMd5sum{$md5sum};
    if(defined $$localByMd5sum{$md5sum}){
      push @backedUp, $file;
    }else{
      push @needsBackUp, $file;
    }
  }

  my $reorganizeCmd = '';
  for my $file(@backedUp){
    my $md5sum = $$byFile{$file};
    my $localFile = $$localByMd5sum{$md5sum};
    my $newRemoteFile = "$remoteDCIM/$localFile";
    my $dir = $newRemoteFile;
    $dir =~ s/\/[^\/]*$//;
    if($dir =~ /$remoteDCIM\/\.\/backup_\d+$/x){
      next;
    }
    $dir = wrapQuotes $dir;
    $file = wrapQuotes $file;
    $newRemoteFile = wrapQuotes $newRemoteFile;
    $reorganizeCmd .= "mkdir -p $dir; mv $file $newRemoteFile;\n";
  }
  if(length $reorganizeCmd > 0){
    print "\n\n$reorganizeCmd";
    system "n9", "-s", $reorganizeCmd;
  }

  print "\n\n===NEEDS BACKUP:\n" . join "\n", @needsBackUp;
  print "===\n\n";

  if(@needsBackUp > 0){
    my $now = `date +\%s`;
    chomp $now;
    my $dir = "$localDCIM/backup_$now";
    system "mkdir -p $dir";
    system "rsync -avP root@`n9`:$remoteDCIM/*.* $dir/";
  }
  system "n9", "-s", "chown user.metadata-users -R $remoteDCIM";
}

sub md5sumMaps(\@){
  my %byMd5sum;
  my %byFile;
  my @md5sumLines = @{shift()};
  for my $line(@md5sumLines){
    if($line =~ /^([0-9a-f]{32})  (.+)\n$/){
      $byFile{$2} = $1;
      $byMd5sum{$1} = $2;
    }
  }
  return (\%byMd5sum, \%byFile);
}

sub runMd5sum($){
  my $file = shift;
  $file = wrapQuotes $file;
  return `md5sum $file`;
}

sub wrapQuotes($){
  my $s = shift;
  $s =~ s/'/'\\''/g;
  $s = "'$s'";
  return $s;
}

sub updateMd5sums(){
  my @lines = `cat $localDCIM/md5sums`;
  my ($byMd5sum, $byFile) = md5sumMaps @lines;

  my $find = "find -false";
  $find .= " -or -iname '*.$_'" foreach @filetypes;
  my @files = `$find`;
  chomp $_ foreach @files;
  @files = grep {not defined $$byFile{$_}} @files;

  print "md5summing local files\n";
  @lines = map {runMd5sum $_} @files;
  print @lines;

  my ($extraByMd5sum, $extraByFile) = md5sumMaps @lines;
  my %newByMd5sum = (%$byMd5sum, %$extraByMd5sum);
  my %newByFile = (%$byFile, %$extraByFile);

  my $out;
  for my $file(keys %newByFile){
    if(-e "$localDCIM/$file"){
      $out .= "$newByFile{$file}  $file\n";
    }else{
      print "removing md5sum for $file\n";
    }
  }

  open FH, "> $localDCIM/md5sums";
  print FH $out;
  close FH;

  return \%newByMd5sum;
}

&main(@ARGV);
