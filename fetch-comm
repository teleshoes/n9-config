#!/usr/bin/perl
use strict;
use warnings;

sub run(@);
sub runOrDie(@);

my $MODE_N9E = "n9e";
my $MODE_SCBR = "scbr";
my $MODE_DEFAULT = "default";

my $defaultSmsMode = $MODE_N9E;
my $defaultCallMode = $MODE_SCBR;

my $usage = "Usage:
  $0 sms [--mode=MODE]
    backup SMS

  $0 call [--mode=MODE]
    backup calls

  MODE:
    $MODE_N9E:      use mode=$MODE_N9E (n9export)
    $MODE_SCBR:     use mode=$MODE_SCBR (smsbackuprestore/callbackuprestore)
    $MODE_DEFAULT:  use mode=$defaultSmsMode for 'sms' and mode=$defaultCallMode for 'call'
";

sub main(@){
  my $type;
  if(@_ > 0 and $_[0] =~ /^(sms|call)$/){
    $type = shift;
  }

  my $mode = $MODE_DEFAULT;
  if(@_ > 0 and $_[0] =~ /^--mode=($MODE_N9E|$MODE_SCBR|$MODE_DEFAULT)$/){
    $mode = $1;
    shift;
  }

  die $usage if not defined $type or @_ > 0;

  $mode = $defaultSmsMode if $mode eq $MODE_DEFAULT and $type eq "sms";
  $mode = $defaultCallMode if $mode eq $MODE_DEFAULT and $type eq "call";

  my $dir = "$ENV{HOME}/Code/n9";
  my $dest = "$dir/backup";
  my $parseTool = "comm-tool.pl $type";

  my $host = `n9`;
  chomp $host;

  my $srcDir = "/home/user/MyDocs/backup-$type";
  my $destDir = "$dest/backup-$type";

  my $remoteRepoDir = "$srcDir-repo";

  if($mode eq $MODE_N9E){
    runOrDie "n9", "-b", "backup-comm-n9e $type";
  }elsif($mode eq $MODE_SCBR){
    my $scbrExec = "${type}backuprestore";
    run "n9", "-s", "killall -9 $scbrExec";
    run "n9", "-s", "/opt/dropcache-mdn/bin/dropcache.sh --3";
    runOrDie "n9", "-b", "backup-comm-scbr $type --retry 20";
  }

  runOrDie "mkdir", "-p", $destDir;
  runOrDie "rsync", "-avP", "root\@$host:$srcDir/", $destDir;
  runOrDie "perl $dir/$parseTool split";
  runOrDie "perl $dir/$parseTool commit";
  runOrDie "perl $dir/$parseTool join-all";

  run "scp", "$destDir/all.$type", "root\@$host:$srcDir/";
  run "n9", "-s", "chown user.users -R $srcDir/all.$type";
  run "rsync", "-a", "$destDir/repo/", "root\@$host:$remoteRepoDir/";
  run "n9", "-s", "chown user.users -R $remoteRepoDir";
}

sub run(@){
  print "@_\n";
  system @_;
}
sub runOrDie(@){
  run @_;
  die "failed" if $? != 0;
}

&main(@ARGV);
